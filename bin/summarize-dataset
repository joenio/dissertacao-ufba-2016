#!/usr/bin/perl
use utf8;
use open ':locale';
use Modern::Perl '2014';
use File::Basename;
use YAML qw( LoadFile Dump );
use lib 'lib';
use Dissertacao qw( unique bibtex_load );

=head1 NAME

summarize-dataset - read dataset and summarize into one single YAML structure

=head1 SYNOPSIS

  ./bin/summarize-dataset <dataset-dir>

=head1 DESCRIPTION

Write to the stdout a multi-document YAML composed by all data for each
project, each software.yml and search/*.bib file are read and loaded in
a YAML structure.

=cut

sub bibtex_project_load {
  my $file = shift;
  my %bib = bibtex_load $file;
  my %data = map { $_ => {title => $bib{$_}->get('title'), doi => $bib{$_}->get('doi')} } keys %bib;
  return \%data;
}

sub load_project_data {
  my $dir = shift;
  my %dataset = %{ LoadFile("$dir/software.yml") };
  ($dataset{software}{short_name}) = split ' - ', $dataset{software}{name};
  $dataset{files}{'paper.bib'} = bibtex_project_load "$dir/paper.bib";
  $dataset{files}{'search/acm.bib'} = bibtex_project_load "$dir/search/acm.bib";
  $dataset{files}{'search/ieee.bib'} = bibtex_project_load "$dir/search/ieee.bib";
  $dataset{references} = \%{ LoadFile("$dir/references.yml") };
  return %dataset;
}

sub load_projects_dataset {
  my $dir = shift;
  my %dataset = ();
  foreach my $d (grep { -d $_ } glob("$dir/*")) {
    my %data = load_project_data($d);
    $dataset{basename $d} = \%data;
  }
  return %dataset;
}

if (@ARGV < 1) {
  warn "E: missing options!";
  say "Usage: $0 <dataset-dir>";
  exit 1;
}
our $DATASET_DIR = shift @ARGV;
my %dataset = load_projects_dataset($DATASET_DIR);
print Dump(%dataset);
