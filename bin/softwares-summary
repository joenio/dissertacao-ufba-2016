#!/usr/bin/perl
use Modern::Perl '2014';
use experimental 'signatures';
use File::Basename;
use YAML qw( LoadFile );
use Text::BibTeX;
use Mojo::Template;
use DateTime::Format::HTTP;
use open ':locale';
use utf8;

sub read_paper_file($filepath) {
    my %paper = ();
    unless (-z $filepath) {
      if (my $bib = Text::BibTeX::File->new($filepath)) {
        while (my $entry = Text::BibTeX::Entry->new({binmode => 'utf-8'}, $bib)) {
          next unless $entry->parse_ok;
          my ($first_author) = $entry->split('author');
          $paper{author} = $first_author;
          $paper{title} = $entry->get('title');
        }
        $bib->close;
      }
    }
    return %paper;
}

sub read_citations_file($filepath) {
  my %citations = ();
  unless (-z $filepath) {
    if (my $bib = Text::BibTeX::File->new($filepath)) {
      $citations{count} = 0;
      while (my $entry = Text::BibTeX::Entry->new($bib)) {
        next unless $entry->parse_ok;
        $citations{count}++;
      }
      $bib->close;
    }
  }
  return %citations;
}

sub load_collected_data($software_dir) {
  # read YAML: software.yml
  my $yaml = LoadFile("$software_dir/software.yml");
  my %software = %{ $yaml };
  # read BIBTEX: paper.bib
  my %paper = read_paper_file("$software_dir/paper.bib");
  $software{paper}{bibfile} = \%paper;
  # read BIBTEX: citations.bib
  my %citations = read_citations_file("$software_dir/citations.bib");
  $software{citations}{summary} = \%citations;

  # check if collected data miss something
  if ($software{features}{download_available} eq 'yes') {
    if ($software{software}{distribution} =~ /^gratis|foss$/) {
      unless (-d "$software_dir/source-code") {
        $software{debug}{source_code_missing} = 1;
      }
      else {
        $software{debug}{source_code_missing} = 0;
      }
    }
  }
  unless (-l "$software_dir/paper.pdf") {
    $software{debug}{symlink_to_paper_missing} = 1;
  }
  else {
    $software{debug}{symlink_to_paper_missing} = 0;
  }

  return %software;
}

sub load_all_collected_data($softwares_dir) {
  my %softwares = ();
  foreach my $dir (grep { -d $_ } glob("$softwares_dir/*")) {
    my %software_data = load_collected_data($dir);
    my $software_key = basename $dir;
    $softwares{$software_key} = \%software_data;

  }
  return %softwares;
}

sub format_collected_data {
  my $softwares = shift;
  foreach (keys %$softwares) {
    my ($searched_at, $date);
    # format IEEEXPLORE searched_at as dd/mm/yyyy
    $searched_at = $softwares->{$_}{citations}{ieeexplore}{searched_at};
    $date = DateTime::Format::HTTP->parse_datetime($searched_at);
    $softwares->{$_}{citations}{ieeexplore}{searched_at} = $date->dmy('/');
    # format ACM searched_at as dd/mm/yyyy
    $searched_at = $softwares->{$_}{citations}{acm}{searched_at};
    $date = DateTime::Format::HTTP->parse_datetime($searched_at);
    $softwares->{$_}{citations}{acm}{searched_at} = $date->dmy('/');
    # format distribution as pt_BR
    if ($softwares->{$_}{software}{distribution} eq 'undefined') {
      $softwares->{$_}{software}{distribution} = '{\it software não disponível}';
    }
    elsif ($softwares->{$_}{software}{distribution} eq 'foss') {
      $softwares->{$_}{software}{distribution} = 'software livre';
    }
    elsif ($softwares->{$_}{software}{distribution} eq 'gratis') {
      $softwares->{$_}{software}{distribution} = 'software gratuito (freeware, demo, etc)';
    }
    # format license as pt_BR
    if ($softwares->{$_}{software}{license} eq 'undefined') {
      $softwares->{$_}{software}{license} = 'sem licença definida';
    }
    else {
      $softwares->{$_}{software}{license} = "sob a licença $softwares->{$_}{software}{license}";
    }
  }
}

if ($#ARGV < 1 || $ARGV[0] ne '-o' || length($ARGV[1]) == 0) {
  warn "E: missing options!";
  print "Usage: $0 -o <output-filename>\n";
  exit 1;
}
our $OUTPUT_FILENAME = $ARGV[1];
our $DATASET_DIR = "$ENV{PWD}/dataset";
my %softwares = load_all_collected_data("$DATASET_DIR/academic-softwares");
format_collected_data \%softwares;
my $t = Mojo::Template->new(auto_escape => 1);
my $output = $t->render_file("$ENV{PWD}/bin/softwares-summary.tex.epl", %softwares);
open my $SUMMARY, '>', $OUTPUT_FILENAME or die $!;
print $SUMMARY $output;
close $SUMMARY;
