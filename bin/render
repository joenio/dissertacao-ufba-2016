#!/usr/bin/perl
use utf8;
use open ':locale';
use Modern::Perl '2014';
use YAML::XS qw( LoadFile );
use Mojo::Template;
use lib 'lib';
use Dissertacao qw( bibtex_load );

if (@ARGV < 1) {
  warn "E: missing options!";
  say "Usage: $0 <template-filename> [--software=<software>]";
  exit 1;
}
my $software = undef;
if ($ARGV[-1] =~ m/^--software=(\S+)/) {
  $software = $1;
}
our $TEMPLATE_FILENAME = shift @ARGV;
die "template file not found: $TEMPLATE_FILENAME." unless -e $TEMPLATE_FILENAME;

my $DATASET_FILENAME = 'cache/dataset.yml';
my $REFERENCES_FILENAME = 'documents/references.bib';
die "dataset file not found: $DATASET_FILENAME." unless -e $DATASET_FILENAME;
die "references file not found: $REFERENCES_FILENAME." unless -e $REFERENCES_FILENAME;

my %dataset = LoadFile "$ENV{PWD}/$DATASET_FILENAME";
my %bib = bibtex_load "$ENV{PWD}/$REFERENCES_FILENAME";

my %references = map { $bib{$_}->get('id') => $bib{$_} } keys %bib;
my $t = Mojo::Template->new(auto_escape => 1);
if ($software) {
  print $t->render_file("$ENV{PWD}/$TEMPLATE_FILENAME", $dataset{$software}, \%references);
}
else {
  print $t->render_file("$ENV{PWD}/$TEMPLATE_FILENAME", \%dataset, \%references);
}
